plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'org.jetbrains.kotlin.android'
}

// 获取配置信息
def configs = rootProject.ext.configs

android {
    compileSdk configs.compileSdkVersion

    defaultConfig {
        minSdk configs.minSdkVersion
        targetSdk configs.targetSdkVersion

        // 设置声网 AppId
        buildConfigField("String", "agoraAppId", configs.agoraAppId)

        // 设置IM Host
        buildConfigField("String", "imHostDebug", configs.imHostDebug)
        buildConfigField("String", "imHostRelease", configs.imHostRelease)
    }

    buildTypes {
        release {
            // 混淆文件
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            consumerProguardFiles 'consumer-rules.pro'
        }
    }

    // 配置 Java 编译版本
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    // 开启 ViewBinding
    buildFeatures {
        viewBinding true
    }

    // 编译忽略错误配置
    lintOptions {
        abortOnError false
    }
    // kotlin 配置
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11
    }
    kapt {
        arguments {
            // Room 配置
            arg("room.schemaLocation", "$projectDir/schemas".toString())
        }
    }
}

dependencies {
    // 数据库在哪里实现，这个就要加在哪里
    kapt "androidx.room:room-compiler:2.3.0"

    // socket.io-client
    implementation ('io.socket:socket.io-client:1.0.2') {
        // excluding org.json which is provided by Android
        exclude group: 'org.json', module: 'json'
    }
    // 声网音视频通话 https://docs.agora.io/cn/Voice/start_call_audio_android?platform=Android
    implementation 'com.github.agorabuilder:native-voice-sdk:3.6.2'

    // 依赖 base 库
    implementation project(':vmbase')
    // 依赖 common 库
    implementation project(':vmcommon')
    // 依赖 data 库
    implementation project(':vmdata')
    // 依赖 db 库
    implementation project(':vmdb')
    // 依赖 gift 库
    implementation project(':vmgift')
    // 依赖 image 库
    implementation project(':vmimage')
    // 依赖 request 库
    implementation project(':vmrequest')
}

apply from: "${rootDir.absolutePath}/arouter.gradle"
