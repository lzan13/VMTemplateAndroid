plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'org.jetbrains.kotlin.android'
}

// 获取配置信息
def configs = rootProject.ext.configs
def signings = rootProject.ext.signings
ext {
    isDebug = true
}
android {
    compileSdk configs.compileSdkVersion

    defaultConfig {
        applicationId configs.applicationId
        minSdk configs.minSdkVersion
        targetSdk configs.targetSdkVersion
        versionCode configs.versionCode
        versionName configs.versionName

        // 简单粗暴解决多个库出现 UnsatisfiedLinkError 文件错误问题
        ndk {
            // "x86", "armeabi-v7a", "arm64-v8a"
//            abiFilters "x86"
//            abiFilters "armeabi-v7a"
            abiFilters "arm64-v8a"
//            abiFilters "armeabi-v7a", "arm64-v8a"
        }

        manifestPlaceholders = [
                appChannel    : "develop",
                appVersionName: configs.versionName,
                isDebug       : isDebug,
        ]
    }

    // 签名信息
    signingConfigs {
        // 签名信息，因为签名文件是保密信息，放在一个配置文件中进行读取，这个文件不会再版本库里存在
        release {
            keyAlias = signings.keyAlias
            keyPassword = signings.keyPassword
            storeFile = file(signings.storeFile)
            storePassword = signings.storePassword
        }
    }

    // 编译配置
    buildTypes {
        debug {
            // 添加参数表示 debug 环境，这里使用测试环境
            isDebug = true
            // 是否开启压缩
            zipAlignEnabled false
            // 是否开启混淆
            minifyEnabled false
            // Debug 打包签名信息，这里和 release 配置相同方便三方登录与分享调试
            signingConfig signingConfigs.release
        }
        release {
            // 添加参数表示 debug 环境，这里使用测试环境
            isDebug = false
            // 是否开启压缩
            zipAlignEnabled false
            // 是否开启混淆
            minifyEnabled false
            // 打包时删除无用资源 依赖于混淆，必须和 minifyEnabled 一起使用
            shrinkResources false
            // 设置正式打包的签名
            signingConfig signingConfigs.release
            // 混淆文件
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            // 修改生成的 apk 名字，格式为:项目-版本-时间-渠道名.apk
            android.applicationVariants.all { variant ->
                variant.outputs.all {
                    outputFileName = "${rootProject.name}V${defaultConfig.versionName}.${getTime()}." + variant.productFlavors[0].name + '.apk'
                }
            }
        }
    }

    // 多渠道打包配置
    flavorDimensions "default"
    productFlavors {
        develop {
            dimension = "default"
            manifestPlaceholders = [
                    appChannel    : "develop",
                    appVersionName: configs.versionName,
                    isDebug       : isDebug
            ]

            // 根据渠道不同加载不同的so库
            ndk {
//                abiFilters "armeabi-v7a"
                abiFilters "arm64-v8a"
//                abiFilters "armeabi-v7a", "arm64-v8a"
            }
        }
        googlePlay {
            dimension = "default"
            manifestPlaceholders = [
                    appChannel    : "develop",
                    appVersionName: configs.versionName,
                    isDebug       : isDebug
            ]

            // GooglePlay 可以发布aab包，所以不需要单独打支持不同CPU的包
            ndk {
                abiFilters "armeabi-v7a", "arm64-v8a"
            }
        }
    }

    // UNIApp SDK 配置必须添加 否则无法正确运行
    aaptOptions {
        additionalParameters '--auto-add-overlay'
        //noCompress 'foo', 'bar'
        ignoreAssetsPattern "!.svn:!.git:.*:!CVS:!thumbs.db:!picasa.ini:!*.scc:*~"
    }
    // 配置 Java 编译版本
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    // 开启 ViewBinding
    buildFeatures {
        viewBinding true
    }
    // 编译忽略错误配置
    lintOptions {
        abortOnError false
    }
    // kotlin 配置
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11
    }
}

dependencies {
    // 依赖 ads 模块
    implementation project(':vmads')
    // 依赖 base 模块
    implementation project(':vmbase')
    // 依赖 common 模块
    implementation project(':vmcommon')
    // 依赖 db 模块
    implementation project(':vmdb')
    // 依赖 data 模块
    implementation project(':vmdata')
    // 依赖 gift 模块
    implementation project(':vmgift')
    // 依赖 im 模块
//    implementation project(':vmim')
    implementation project(':vmimem')
    // 依赖 image 模块
    implementation project(':vmimage')
    // 依赖 pay 模块
    implementation project(':vmpay')
    // 依赖 mp 模块
    implementation project(':vmmp')
    // 依赖 push 模块
//    implementation project(':vmpush')
    // 依赖 qr 模块
    implementation project(':vmqr')
    // 依赖 report 模块
    implementation project(':vmreport')
    // 依赖 request 模块
    implementation project(':vmrequest')
}

// 获取当前系统时间
def getTime() {
    return new Date().format("yyyyMMddHHmm", TimeZone.getTimeZone("UTC"))
}

apply from: "${rootDir.absolutePath}/arouter.gradle"